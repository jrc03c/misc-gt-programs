--------------------------------------------------------------------------------
-- @jrc03c/mailgun/send
--------------------------------------------------------------------------------
-- url    : https://www.guidedtrack.com/programs/31600/edit
-- author : Josh Castle (joshrcastle@gmail.com)
--------------------------------------------------------------------------------
-- inputs:
--
--    `body_html` = a string representing the HTML body of the email to be sent
--
--    `body_text` = a string representing the plaintext body of the email to be
--        sent
--
--    `domain_name` = a string representing the domain name from which the email
--        will be sent
--
--    `from_address` = a string representing the email address from which the
--        email will be sent
--
--    `subject` = a string representing the subject line of the email to be sent
--
--    `to_address` = a string representing the email address to which the email
--        will be sent
--
-- outputs:
--
--     `was_sent` = "yes" or "no"
--
--     `mailgun_send_error` = an association containing information about any
--         errors returned from the `*service` call
--------------------------------------------------------------------------------
-- SERVICE SETTINGS:
-- Name: Mailgun
-- URL: https://api.mailgun.net/v3/
-- Username: api
-- Password: YOUR_API_KEY
--------------------------------------------------------------------------------
-- MAILGUN API DOCUMENTATION:
-- https://documentation.mailgun.com/docs/mailgun/api-reference/openapi-final/tag/Messages/#tag/Messages/operation/POST-v3--domain-name--messages
--------------------------------------------------------------------------------

>> error_title = "@jrc03c/mailgun/send"

---

*if: not body_html
	>> error_message = "You must define a variable called <code>body_html</code> that is a string representing the HTML body of the email to be sent!"
	*program: @jrc03c/show-error

*if: not (body_html.type = "string")
	>> error_message = "The variable called <code>body_html</code> must have a string value representing the HTML body of the email to be sent! (The value you provided is a(n) {body_html.type}.)"
	*program: @jrc03c/show-error

---

*if: not body_text
	>> error_message = "You must define a variable called <code>body_text</code> that is a string representing the plaintext body of the email to be sent!"
	*program: @jrc03c/show-error

*if: not (body_text.type = "string")
	>> error_message = "The variable called <code>body_text</code> must have a string value representing the plaintext body of the email to be sent! (The value you provided is a(n) {body_text.type}.)"
	*program: @jrc03c/show-error

---

*if: not domain_name
	>> error_message = "You must define a variable called <code>domain_name</code> that is a string representing the domain name from which the email will be sent!"
	*program: @jrc03c/show-error

*if: not (domain_name.type = "string")
	>> error_message = "The variable called <code>domain_name</code> must have a string value representing the domain name from which the email will be sent! (The value you provided is a(n) {domain_name.type}.)"
	*program: @jrc03c/show-error

---

*if: not from_address
	>> error_message = "You must define a variable called <code>from_address</code> that is a string representing the email address from which the email will be sent!"
	*program: @jrc03c/show-error

*if: not (from_address.type = "string")
	>> error_message = "The variable called <code>from_address</code> must have a string value representing the email address from which the email will be sent! (The value you provided is a(n) {from_address.type}.)"
	*program: @jrc03c/show-error

---

*if: not subject
	>> error_message = "You must define a variable called <code>subject</code> that is a string representing the subject line of the email to be sent!"
	*program: @jrc03c/show-error

*if: not (subject.type = "string")
	>> error_message = "The variable called <code>subject</code> must have a string value representing the subject line of the email to be sent! (The value you provided is a(n) {subject.type}.)"
	*program: @jrc03c/show-error

---

*if: not to_address
	>> error_message = "You must define a variable called <code>to_address</code> that is a string representing the email address to which the email will be sent!"
	*program: @jrc03c/show-error

*if: not (to_address.type = "string")
	>> error_message = "The variable called <code>to_address</code> must have a string value representing the email address to which the email will be sent! (The value you provided is a(n) {to_address.type}.)"
	*program: @jrc03c/show-error

---

>> body_html = body_html.encode("URL")
>> body_text = body_text.encode("URL")
>> domain_name = domain_name.encode("URL")
>> from_address = from_address.encode("URL")
>> subject = subject.encode("URL")
>> to_address = to_address.encode("URL")

>> was_sent = "no"
>> mailgun_send_error = {}

>> path = "/{domain_name}/messages?"
>> path = "{path}html={body_html}"
>> path = "{path}&text={body_text}"
>> path = "{path}&domain_name={domain_name}"
>> path = "{path}&from={from_address}"
>> path = "{path}&subject={subject}"
>> path = "{path}&to={to_address}"

*service: Mailgun
	*method: POST
	*path: {path}
	*success
		>> was_sent = "yes"
	*error
		>> mailgun_send_error = {}
		>> mailgun_send_error["status"] = it["code"]
		>> mailgun_send_error["message"] = it["message"]

>> body_html = ""
>> body_text = ""
>> domain_name = ""
>> from_address = ""
>> it = ""
>> path = ""
>> subject = ""
>> to_address = ""